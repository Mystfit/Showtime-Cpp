message(STATUS "Configuring ${UNREAL_TARGET} target")

# Set Unreal project paths
set(UNREAL_EXECUTABLE $ENV{PROGRAMFILES}/Unreal/Editor/Unreal.exe CACHE FILEPATH "Path to Unreal executable")
set(UNREAL_SRC_DIR ${CMAKE_CURRENT_LIST_DIR}/ShowtimeUnreal)
set(UNREAL_BUILD_DIR ${CMAKE_SWIG_ROOT_OUTDIR}/unreal/ShowtimeUnreal)
set(UNREAL_BIN_DIR ${UNREAL_BUILD_DIR}/external/bin/${CMAKE_GENERATOR_PLATFORM})
set(UNREAL_LIB_DIR ${UNREAL_BUILD_DIR}/external/lib/${CMAKE_GENERATOR_PLATFORM})
set(UNREAL_INCLUDE_DIR ${UNREAL_BUILD_DIR}/external/include)

# Path vars
set(UNREAL_DEPENDENCIES ${SHARED_FROM_STATIC_TARGET} ${CORE_ENTITIES_TARGET})

# Create destination directories
file(MAKE_DIRECTORY ${UNREAL_BUILD_DIR})
file(MAKE_DIRECTORY ${UNREAL_BIN_DIR})
file(MAKE_DIRECTORY ${UNREAL_LIB_DIR})

# Unreal target build package
add_custom_target(${UNREAL_TARGET} ALL DEPENDS ${UNREAL_BUILD_DIR}/ShowtimeUnreal.uplugin)

# Unreal project template
add_custom_command(
    PRE_BUILD
    TARGET ${UNREAL_TARGET}
    COMMAND ${CMAKE_COMMAND} -E echo "Copying ${UNREAL_SRC_DIR} to ${UNREAL_BUILD_DIR}"
    COMMAND ${CMAKE_COMMAND} -E copy_directory ${UNREAL_SRC_DIR} ${UNREAL_BUILD_DIR}
)

add_custom_command(
    PRE_BUILD
    TARGET ${UNREAL_TARGET}
    COMMAND ${CMAKE_COMMAND} -E echo "Copying ${Showtime_ALL_INCLUDE_DIRS} to ${UNREAL_INCLUDE_DIR}"
    COMMAND ${CMAKE_COMMAND} -E copy_directory ${Showtime_ALL_INCLUDE_DIRS} ${UNREAL_INCLUDE_DIR}
)

add_custom_command(
    PRE_BUILD
    TARGET ${UNREAL_TARGET}
    COMMAND ${CMAKE_COMMAND} -E echo "Copying ${Showtime_COMPILED_INCLUDE_DIRS} to ${UNREAL_INCLUDE_DIR}"
    COMMAND ${CMAKE_COMMAND} -E copy_directory ${Showtime_COMPILED_INCLUDE_DIRS} ${UNREAL_INCLUDE_DIR}
)

# Bundle copying
if(MACOSX OR BUILD_MACOS_CONTAINER)
    # Copy mac libs
    # Do stuff here

    if(BUILD_MACOS_CONTAINER)
        list(APPEND UNREAL_DEPENDENCIES ${MACOS_CONTAINER_TARGET})
    endif()
endif()

# Android lib copying
if(ANDROID OR BUILD_ANDROID_CONTAINER)
    set(ANDROID_UNREAL_BIN_DIR ${UNREAL_BIN_DIR}/Android/${ANDROID_ABI})
    set(ANDROID_UNREAL_LIB_DIR ${UNREAL_LIB_DIR}/Android/${ANDROID_ABI})
    add_custom_command(
        PRE_BUILD
        TARGET ${UNREAL_TARGET}
        COMMAND ${CMAKE_COMMAND} -E make_directory ${ANDROID_UNREAL_BIN_DIR}
        COMMAND ${CMAKE_COMMAND} -E echo "Copying ${ANDROID_UNREAL_BIN_DIR}"
        COMMAND ${CMAKE_COMMAND} -E copy_directory ${ANDROID_BIN_DIR} ${ANDROID_UNREAL_BIN_DIR}
        COMMAND ${CMAKE_COMMAND} -E echo "Copying ${ANDROID_UNREAL_LIB_DIR}"
        COMMAND ${CMAKE_COMMAND} -E copy_directory ${ANDROID_LIB_DIR} ${ANDROID_UNREAL_LIB_DIR}
        DEPENDS ${ANDROID_CONTAINER_TARGET}
    )

    # Copy plugins
    add_custom_command(
        PRE_BUILD
        TARGET ${UNREAL_TARGET}
        COMMAND ${CMAKE_COMMAND} -E make_directory ${ANDROID_UNREAL_BIN_DIR}/plugins
        COMMAND ${CMAKE_COMMAND} -E echo "Copying plugins to ${ANDROID_UNREAL_BIN_DIR}/plugins"
        COMMAND ${CMAKE_COMMAND} -E copy_directory ${ANDROID_BIN_DIR}/plugins ${ANDROID_UNREAL_BIN_DIR}/plugins
        DEPENDS ${ANDROID_CONTAINER_TARGET}
    )
    if(BUILD_ANDROID_CONTAINER)
        list(APPEND UNREAL_DEPENDENCIES ${ANDROID_CONTAINER_TARGET})
    endif()
endif()

# Windows lib copying
if(WIN32 OR BUILD_WINDOWS_CONTAINER)
    # Copy windows platform files
    set(WIN_UNREAL_BIN_DIR ${UNREAL_BIN_DIR}/Win64/${CMAKE_GENERATOR_PLATFORM})
    set(WIN_UNREAL_LIB_DIR ${UNREAL_LIB_DIR}/Win64/${CMAKE_GENERATOR_PLATFORM})
    add_custom_command(
        PRE_BUILD
        TARGET ${UNREAL_TARGET}
        COMMAND ${CMAKE_COMMAND} -E make_directory ${WIN_UNREAL_BIN_DIR}
        COMMAND ${CMAKE_COMMAND} -E make_directory ${WIN_UNREAL_BIN_DIR}/plugins
        COMMAND ${CMAKE_COMMAND} -E make_directory ${WIN_UNREAL_LIB_DIR}
        COMMAND ${CMAKE_COMMAND} -E echo "Copying Windows platform files to ${WIN_UNREAL_BIN_DIR}"
        COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE:${SHARED_FROM_STATIC_TARGET}> ${WIN_UNREAL_BIN_DIR}/$<TARGET_FILE_NAME:${SHARED_FROM_STATIC_TARGET}>
        COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_LINKER_FILE:${SHARED_FROM_STATIC_TARGET}> ${WIN_UNREAL_LIB_DIR}/$<TARGET_LINKER_FILE_NAME:${SHARED_FROM_STATIC_TARGET}>
        COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_PDB_FILE:${SHARED_FROM_STATIC_TARGET}> ${WIN_UNREAL_BIN_DIR}/$<TARGET_PDB_FILE_NAME:${SHARED_FROM_STATIC_TARGET}>
        COMMAND ${CMAKE_COMMAND} -E copy_directory ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/plugins ${WIN_UNREAL_BIN_DIR}/plugins
        DEPENDS ${WINDOWS_CONTAINER_TARGET} ${SHARED_FROM_STATIC_TARGET}
    )

    if(BUILD_WINDOWS_CONTAINER)
        list(APPEND UNREAL_DEPENDENCIES ${WINDOWS_CONTAINER_TARGET})
    endif()
    if(BUILD_ANDROID_CONTAINER)
        list(APPEND UNREAL_DEPENDENCIES ${ANDROID_CONTAINER_TARGET})
    endif()
endif()

add_dependencies(${UNREAL_TARGET} ${UNREAL_DEPENDENCIES})
#install(FILES ${Unreal_PACKAGE_PATH} DESTINATION Unreal)
