message(STATUS "Configuring ${WINDOWS_CONTAINER_TARGET} target")

# Set file and folder vars
set(WINDOWS_CONTAINER_BUILD_DIR "${CMAKE_CURRENT_BINARY_DIR}/windows-container")
set(WINDOWS_CONTAINER_NAME "windows-builder")

set(WINDOWS_CONTAINER_BUILD_DIR_NAME vagrant-showtime-build)
set(WINDOWS_CONTAINER_INTERNAL_BUILD_DIR "C:/Users/vagrant/${WINDOWS_CONTAINER_BUILD_DIR_NAME}")
set(WINDOWS_CONTAINER_RELATIVE_BIN_PATH "${WINDOWS_CONTAINER_BUILD_DIR_NAME}/bin")
set(WINDOWS_CONTAINER_RELATIVE_LIB_PATH "${WINDOWS_CONTAINER_BUILD_DIR_NAME}/lib")

# Create native paths
file(TO_NATIVE_PATH ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/windows NATIVE_BINARY_PATH)
file(TO_NATIVE_PATH ${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/windows NATIVE_LIBRARY_PATH)

# Create output directories
file(MAKE_DIRECTORY ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/windows)
file(MAKE_DIRECTORY ${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/windows)

# Copy vagrantfile to build dir so we don't pollute the source tree with Vagrant images
configure_file(${CMAKE_CURRENT_LIST_DIR}/Vagrantfile.in ${WINDOWS_CONTAINER_BUILD_DIR}/Vagrantfile)

add_custom_command(
    OUTPUT ${WINDOWS_CONTAINER_OUTPUT_LIB}
    COMMAND ${CMAKE_COMMAND} -E echo "Building Showtime using Windows container"
    COMMAND ${VAGRANTCOMMAND} up
    COMMAND ${CMAKE_COMMAND} -E echo "Copying files from container"
    COMMAND ${VAGRANTCOMMAND} scp ${WINDOWS_CONTAINER_NAME}:${WINDOWS_CONTAINER_RELATIVE_BIN_PATH}/* ${NATIVE_BINARY_PATH}
    COMMAND ${VAGRANTCOMMAND} scp ${WINDOWS_CONTAINER_NAME}:${WINDOWS_CONTAINER_RELATIVE_LIB_PATH}/* ${NATIVE_LIBRARY_PATH}
    COMMAND ${VAGRANTCOMMAND} halt
    WORKING_DIRECTORY ${WINDOWS_CONTAINER_BUILD_DIR}
    VERBATIM
)
