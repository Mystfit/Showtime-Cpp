message(STATUS "Configuring ${ANDROID_CONTAINER_TARGET} target")

# Folder and file vars
set(ANDROID_CONTAINER_NAME "android-builder")
set(ANDROID_BUILD_DIR "${CONTAINER_DIR}/android")
set(ANDROID_CONTAINER_BUILD_DIR_NAME vagrant-showtime-build)
set(ANDROID_CONTAINER_INTERNAL_BUILD_DIR "/${ANDROID_CONTAINER_BUILD_DIR_NAME}")
set(ANDROID_CONTAINER_RELATIVE_BIN_PATH "${ANDROID_CONTAINER_INTERNAL_BUILD_DIR}/bin/Android")
set(ANDROID_CONTAINER_RELATIVE_LIB_PATH "${ANDROID_CONTAINER_INTERNAL_BUILD_DIR}/lib/Android")

# Android build options
set(ANDROID_PLATFORM_VERSIONS
	"29"
	"28"
	"27"
	"26"
	"25"
	"24"
)

set(ANDROID_NDK_VERSIONS
	"r22"
	"r21d"
	"r21b"
	"r21"
	"r20b"
	"r19c"
	"r18b"
	"r17c"
)

set(ANDROID_BOOST_VERSIONS
	"1.72.0"
	"1.70.0"
	"1.69.0"
)

set(ANDROID_PLATFORM "29" CACHE STRING "Android platform version")
set(ANDROID_NDK_VERSION "r22" CACHE STRING "Android NDK version")
set(ANDROID_BOOST_VERSION "1.72.0" CACHE STRING "Boost version")

# Create native paths
file(TO_NATIVE_PATH ${ANDROID_BIN_DIR} NATIVE_BINARY_PATH)
file(TO_NATIVE_PATH ${ANDROID_LIB_DIR} NATIVE_LIBRARY_PATH)

# Create output directories
file(MAKE_DIRECTORY ${ANDROID_BIN_DIR})
file(MAKE_DIRECTORY ${ANDROID_LIB_DIR})

set_property(CACHE ANDROID_PLATFORM PROPERTY STRINGS ${ANDROID_PLATFORM_VERSIONS})
set_property(CACHE ANDROID_NDK_VERSION PROPERTY STRINGS ${ANDROID_NDK_VERSIONS})
set_property(CACHE ANDROID_BOOST_VERSION PROPERTY STRINGS ${ANDROID_BOOST_VERSIONS})
set_property(CACHE ANDROID_ABI PROPERTY STRINGS ${ANDROID_ABI_VERSIONS})


# Copy vagrantfile to build dir so we don't pollute the source tree with Vagrant images
configure_file(${CMAKE_CURRENT_LIST_DIR}/Vagrantfile.in ${ANDROID_BUILD_DIR}/Vagrantfile)

add_custom_command(
    OUTPUT ${ANDROID_OUTPUT_LIB}
    COMMAND ${CMAKE_COMMAND} -E echo "Building Showtime for Android using Linux container"
    COMMAND ${VAGRANTCOMMAND} up
    COMMAND ${CMAKE_COMMAND} -E echo "Copying files from container"
    COMMAND ${VAGRANTCOMMAND} scp ${ANDROID_CONTAINER_NAME}:${ANDROID_CONTAINER_RELATIVE_BIN_PATH}/* ${NATIVE_BINARY_PATH}
    COMMAND ${VAGRANTCOMMAND} scp ${ANDROID_CONTAINER_NAME}:${ANDROID_CONTAINER_RELATIVE_LIB_PATH}/* ${NATIVE_LIBRARY_PATH}
    COMMAND ${VAGRANTCOMMAND} halt
    WORKING_DIRECTORY ${ANDROID_BUILD_DIR}
    VERBATIM
)
